real expexponential_lpdf(real y, real beta) {
    // If exp(y) is exponentially distributed, then y is exp-exponential distributed.
    // Equivalently, if y is exponentially distributed, then log(y) is exp-exponential
    // distributed.
    return log(beta) - beta * exp(y) + y;
}

real expexponential_lpdf(vector y, vector beta) {
    // Vectorized version of the exp-exponential log probability density function
    return sum(log(beta) - beta .* exp(y) + y);
}

real expexponential_lpdf(vector y, real beta) {
    // Vectorized version of the exp-exponential log probability density function
    // with a single beta
    return sum(log(beta) - beta * exp(y) + y);
}

real expexponential_rng(real beta) {
    // Generates a random sample from the exp-exponential distribution. This involves
    // generating a sample from the exponential distribution and then taking its
    // logarithm.
    return log(exponential_rng(beta));
}

array[] real expexponential_rng(vector beta) {
    return log(exponential_rng(beta));
}