// Copyright (c) Microsoft Corporation.
// Licensed under the MIT license.

real expexponential_lpdf(real y, real beta) {
    // If exp(y) is exponentially distributed, then y is exp-exponential distributed.
    // Equivalently, if y is exponentially distributed, then log(y) is exp-exponential
    // distributed.
    return exponential_lpdf(exp(y) | beta) + y;
}

real expexponential_lpdf(vector y, vector beta) {
    // Vectorized version of the exp-exponential log probability density function
    return exponential_lpdf(exp(y) | beta) + sum(y);
}

real expexponential_lpdf(vector y, real beta) {
    // Vectorized version of the exp-exponential log probability density function
    // with a single beta
    return exponential_lpdf(exp(y) | beta) + sum(y);
}

real expexponential_rng(real beta) {
    // Generates a random sample from the exp-exponential distribution. This involves
    // generating a sample from the exponential distribution and then taking its
    // logarithm.
    return log(exponential_rng(beta));
}

array[] real expexponential_rng(vector beta) {
    return log(exponential_rng(beta));
}